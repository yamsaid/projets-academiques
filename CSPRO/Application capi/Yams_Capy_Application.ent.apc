{Application 'YAMS_CAPY_APPLICATION' logic file generated by CSPro}

PROC GLOBAL
string cas_deces = "non";
numeric agCM = 0;



PROC DATA_FF

PROC DATA_LEVEL
preproc
S0Q5=edit("99:99:99",systime("HHMMSS"));


S0Q4=edit("99/99/9999",sysdate("DDMMYYYY"));



// heure de fin
postproc

S0Q6=edit("99:99:99",systime("HHMMSS"));
PROC S0Q4
PREPROC
noinput;
PROC S0Q5
PREPROC
noinput;
PROC S0Q6
PREPROC
noinput;
PROC S1Q1
preproc
cas_deces="non";


PROC S1Q2
POSTPROC
if length(strip($))<=4 then
errmsg("Le nom doit avoir au moins 4 caractères");
reenter;
endif;

PROC S1Q3
postproc
if length(maketext($)) < 8 then
errmsg("Le numéro doit être de 8 chiffre !");
reenter;
endif;
PROC S1Q4

POSTPROC 
//  VERIFIER QUE 'AUTRE TYPE DE LOGEMENT' n'est pas choisi
IF $ <> 5 THEN // le $ indique la variable courante

// sauter la question autre 'AUTRE TYPE DE LOGEMENT' et aller à S2Q2

SKIP TO S1Q5;

ENDIF;
PROC S1Q4_A
POSTPROC
if length(strip($))<4 then
reenter;
endif;
PROC S1Q5

PROC S1Q5_A

preproc
ask if S1Q5=4;

postproc
if length(strip($))<=4 then
reenter;
endif;


PROC S1Q6



PROC S2Q1

preproc

$=curocc();
noinput;
PROC S2Q2
PREPROC
if curocc() = 1 then
$=S1Q2;
noinput;
endif;

postproc
if curocc() = 1 and $ <> S1Q2 then
errmsg("Le nom du chef de ménage ( %s ) est different au nom ( %s ) déjà enrégistré plus haut !", $, S1Q2)
select("Corrigez ce nom %s ", $ ,"Corrigez ce nom %s enrégistré plus haut",  S1Q2);
endif;

if length(strip($))<=4 then
errmsg("Le nom doit avoir au moins 4 caractères");
reenter;
endif;
PROC S2Q2L
PREPROC
if curocc() = 1 then
$=1;
noinput;
endif;



POSTPROC
if curocc() = 1 and $<> 1 then
errmsg("Veuillez commencer par le Chef du menage svp ! ");
reenter;
endif;

// eviter un CM à une autre ligne

if curocc() > 1 and $ = 1 then
	errmsg(" Le chef du menage a été déjà enregistré ") // 
	select("Corriger le nom de l'individu" , S2Q2, "Corriger le lien " ,$);
endif;
PROC S2Q3
postproc
if S2Q2L=9 and $=2 then
errmsg("Un homme n'est peux pas avoir de co-épouse")
select("Corriger le sexe " ,$,"Corriger le lien",S2Q2L);
endif;
PROC S2Q4
POSTPROC
IF $<0 THEN
errmsg( "L'âge de %s doit être un entier positif!",S2Q2(curocc()));
reenter;
ENDIF;

if S2Q2L=1 then
	agCM=$;
	if $ < 15 then
		errmsg("Le CM ( %s ) doit avoir au moins 15 ans ", S2Q2(curocc()));
		reenter;
	endif;
elseif S2Q2L =3 and (agCM-$) < 15 then
errmsg("L'ecart d'âge entre l'enfant ( %s ) et le CM ( %s ) doit être superieur à 15 ans ",S2Q2(curocc()), S2Q2(1));
reenter;
elseif S2Q2L =5 and (agCM-$) < 15 then
errmsg("L'ecart d'âge entre  %s et le CM ( %s ) doit être superieur à 15 ans ",S2Q2(curocc()), S2Q2(1));
reenter;
elseif S2Q2L in 2 ,6 ,9 and S2Q4 < 12  then
errmsg("%s a %d ans. Un enfant de moins de 12 ans ne peux pas se marier ",S2Q2(curocc()), $)
select( "Corriger l'age  ", $, "Corriger le lien ", S2Q2L, "Ignorer l'erreur" continue)
endif;
PROC S2Q5
postproc
if S2Q4 < 12 and not($ in 1,5) then
errmsg("%s a %d ans. Un enfant de moins de 12 ans ne peux pas se marier ",S2Q2(curocc()), S2Q4 )
select( "Corriger l'age  ", S2Q4, "Corriger le lien ", S2Q2L, "Ignorer l'erreur" continue)
endif;
PROC S2Q5_A


PREPROC 
// VERIFICATION DE LA CONDITION
IF S2Q5 <> 5 THEN
// SAUT DE LA QUESTION EN QUESTION
SKIP TO S2Q6;

ENDIF;
PROC S2Q6

POSTPROC 
//  VERIFIER QUE 'AUTRE TYPE DE LOGEMENT' n'est pas choisi
IF $ <> 5 THEN // le $ indique la variable courante

// sauter la question autre 'AUTRE TYPE DE LOGEMENT' et aller à S2Q2

SKIP TO S2Q7;

ENDIF;
PROC S2Q6_A
if length(strip($))<=4 then
reenter;
endif;
PROC S2Q7_A

PREPROC
ASK IF S2Q7=9;

PROC S2Q8

PREPROC

ASK IF S2Q4 > 16;


PROC S2Q8_A
preproc 
if S2Q8<>5 then
	if accept("Y a - t-il un autre membre ","Oui", "Non") = 2 then
		endgroup;
	endif;
endif;

ask if S2Q8=5;

postproc 
if accept("Y a - t-il un autre membre ","Oui", "Non") = 2 then
	endgroup;
endif;

PROC S3Q1
PREPROC
// totocc($): donne le nombre d'occurence ie nombre d'enregistrement(personne)
// totocc(S2Q1): donne le nombre d'occurence ie nombre d'enregistrement(personne) dans la section composition du menage
// si le nb d'occurence depasse le nombre de personnes enregistrées, quitter le groupe

// le numero de la personne sur la liste du menage

$=S2Q1(curocc());
noinput;



PROC S3Q3

PREPROC
// Demander si la personne a effectué le test
ASK IF S3Q2 = 1;

POSTPROC
// aller à la derniere question si le resultat du test est dispo
IF $ <> 3 THEN
SKIP TO S3Q8;
endif;
PROC S3Q7
postproc
if $ > 30 then
errmsg("La durée de convalescence doit etre moins de 30 jours");
reenter;
endif;
PROC S3Q8
preproc

numeric val;
if S3Q2 = 1 and S3Q3 <> 3 THEN
	val= 99;
endif;


if val = 99 then

if totocc($) >= totocc(S2Q1) then
	if accept("Y a - t-il un décès lié à la dengue ? ","Oui", "Non") = 1 then
		cas_deces="oui" ;
		
	endif;
	endgroup;
endif;
endif;

ask if val <> 99 ;

postproc
if val <> 99 then


if totocc($) >= totocc(S2Q1) then
	if accept("Y a - t-il un décès lié à la dengue ? ","Oui", "Non") = 1 then
		cas_deces="oui" ;
	endif;
	endgroup;
endif;
endif; 

//ask if val<> 99;

PROC S4Q1

preproc

if cas_deces = "non" then
endgroup;
else
$=curocc();
noinput;
endif;


PROC S4Q3


PROC S4Q4
postproc 
if accept("Y a - t-il un autre décès lié à la dengue ? ","Oui", "Non") = 2 then	
	endgroup;
endif;

if $<0 then
errmsg("L'âge doit être un entier positif");
reenter;
endif;


